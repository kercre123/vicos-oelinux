inherit androidmk qlicense qprebuilt androidmk-clang

DESCRIPTION = "MM Camera libraries for MSM/QSD"
SECTION  = "camera"

FILESPATH  =+ "${WORKSPACE}/camera/services/mm-camera/mm-camera2/:"
SRC_URI    = "file://media-controller/mct/debug \
              file://log_debug"

SRC_DIR = "${WORKSPACE}/camera/services/mm-camera/mm-camera2/media-controller/mct/debug"

SRCREV = "${AUTOREV}"
S      = "${WORKDIR}"

DEPENDS += "glib-2.0"
DEPENDS += "libxml2"
DEPENDS += "adreno200"
DEPENDS += "camera"

EXTRA_OEMAKE += "SRC_CAMERA_HAL_DIR='${TMPDIR}'/work/'${MULTIMACH_TARGET_SYS}'/'${MLPREFIX}'camera/'${EXTENDPE}${PV}-${PR}'/lib"
EXTRA_OEMAKE += "QC_PROP_ROOT='${WORKSPACE}'/camera/services"

CFLAGS += "-Wno-error -Wno-uninitialized -Wno-error=attributes -Wno-error=unused-parameter"
CFLAGS += "-Wno-error=builtin-macro-redefined -Wno-error=type-limits"
CFLAGS += "-D__unused="__attribute__((__unused__))""
CFLAGS += "-D_GNU_SOURCE"
CFLAGS += "-DUNIX_PATH_MAX=108"
CFLAGS += "-DIOT_PLATFORM"

CFLAGS += "-I${STAGING_INCDIR}/adreno/"
CFLAGS += "-I${STAGING_INCDIR}/glib-2.0"
CFLAGS += "-I${STAGING_LIBDIR}/glib-2.0/include"

CFLAGS += "-include linux/limits.h"
CFLAGS += "-include stddef.h"
CFLAGS += "-include stdint.h"
CFLAGS += "-Dstrlcpy=g_strlcpy"
CFLAGS += "-Dstrlcat=g_strlcat"
CFLAGS += "-include glib.h"
CFLAGS += "-include glibconfig.h"
CFLAGS += "-include sys/ioctl.h"
CFLAGS += "-include time.h"
CFLAGS += "-include sys/time.h"

LDFLAGS += "-lcutils"
LDFLAGS += "-lglib-2.0"
LDFLAGS += "-llog"
LDFLAGS += "-lrt"
LDFLAGS += "-lxml2"
LDFLAGS += "-lz"

do_fixup_before_compile () {
    # Replace all the $(LOCAL_PATH)/../../../hardware/qcom/camera
    find ${S}/ -type f -name "*.mk" -exec sed -i 's/\$.*\/hardware\/qcom\/camera/\$\(SRC_CAMERA_HAL_DIR\)/g' {} +

    # Replace all the hardware/qcom/camera
    find ${S}/ -type f -name "*.mk" -exec sed -i 's/hardware\/qcom\/camera/\$\(SRC_CAMERA_HAL_DIR\)/g' {} +

    # Remove extra .pc generated by do_patch
    rm -rf ${S}/.pc
    if [ ! -d "${S}/media-controller/mct" ]; then
        mkdir -p ${S}/media-controller/mct
        mv ${S}/media-controller/debug ${S}/media-controller/mct
    fi
}
addtask fixup_before_compile after do_patch before do_configure

export TARGET_LIBRARY_SUPPRESS_LIST="libcamera_client libhardware \
        libsync libui libcamera_metadata libqdMetaData libqservice \
        libbinder libgui libstlport libandroid libxml2 libz"

do_compile () {
    # Current support is limited to 32-bit build
    #
    if [ "${MLPREFIX}" == "lib32-" ] || [ "${MLPREFIX}" == "" -a "${TUNE_ARCH}" == "arm" ]; then
        use_clang_android
        androidmk_setenv
        oe_runmake -f ${LA_COMPAT_DIR}/build/core/main.mk BUILD_MODULES_IN_PATHS=${S} \
            all_modules SHOW_COMMANDS=true || die "make failed"
    else
        die "64-bit build not supported"
    fi
}

do_install () {
    androidmk_setenv

    export TARGET_OUT_HEADERS=${D}${includedir}
    export TARGET_OUT_VENDOR_SHARED_LIBRARIES=${D}${libdir}
    export TARGET_OUT_SHARED_LIBRARIES=${D}${libdir}
    export TARGET_OUT_EXECUTABLES=${D}/system/bin
    export TARGET_OUT_ETC=${D}/system/etc
    export TARGET_OUT=${D}/system

    oe_runmake -f ${LA_COMPAT_DIR}/build/core/main.mk BUILD_MODULES_IN_PATHS=${S} \
        all_modules SHOW_COMMANDS=true USE_INSTALL=true || die "make failed"

    install -d ${D}/usr/src
}

PACKAGES = "${PN}"
FILES_${PN} += "/usr/*"
